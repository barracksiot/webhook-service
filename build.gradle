/*
 * MIT License
 *
 * Copyright (c) 2017 Barracks Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    ext {
        springBootVersion = '1.5.1.RELEASE'
        gradleDockerVersion = '1.2'
        branch = (System.getenv("BRANCH_NAME") ?: "LOCAL")
        buildNumber = (System.getenv("BUILD_NUMBER") ?: "SNAPSHOT")
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "se.transmode.gradle:gradle-docker:${gradleDockerVersion}"
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
}

plugins {
    id "net.linguica.maven-settings" version "0.5"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'org.asciidoctor.convert'

group = projectGroup

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "io.barracks.webhookservice.Application"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
    maven {
        name 'barracks'
        url "https://dl.bintray.com/barracksiot/libs/"
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.10'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-amqp', version: springBootVersion

    compile group: 'io.barracks.libs', name: 'common-libs', version: '2.0.0-1'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.springframework.amqp', name: 'spring-rabbit-test', version: '1.7.0.RELEASE'
    testCompile group: 'org.springframework.amqp', name: 'spring-rabbit-junit', version: '1.7.0.RELEASE'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile group: 'io.arivera.oss', name:'embedded-rabbitmq', version: '1.1.1'
    testCompile group: 'io.barracks.libs', name: 'common-test-libs', version: '2.0.0-1'
}

jar {
    version = branch + '-' + buildNumber
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

jacoco {
    toolVersion = "0.7.8"
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

jacocoTestReport {
    executionData = fileTree(dir: "$buildDir/jacoco", include: '**/*.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task mergeTestReports(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests")
    reportOn files("$buildDir/test-results/binary/test")
}

check.dependsOn jacocoTestReport
check.dependsOn mergeTestReports

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task copyJar(type: Copy, dependsOn: build) {
    from jar
    into "build/docker"
}

task dist(type: Docker, dependsOn: copyJar) {
    push = false
    tagVersion = jar.version
    dockerfile = file('Dockerfile')
}

task deployVersion(type: Docker, dependsOn: copyJar) {
    push = true
    tagVersion = jar.version
    dockerfile = file('Dockerfile')
}

task deployLatest(type: Docker, dependsOn: copyJar) {
    push = true
    tagVersion = branch
    dockerfile = file('Dockerfile')
}

task deploy {
    dependsOn deployVersion, deployLatest
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}
